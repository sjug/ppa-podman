# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>

pkgbase=podman
pkgname=(podman podman-docker)
pkgver=6.1.1
pkgrel=1
pkgdesc='A tool for managing OCI containers and pods.'
arch=(x86_64)
url='https://github.com/podman-container-tools/podman'
license=(Apache-2.0)
makedepends=(
  apparmor
  btrfs-progs
  catatonit
  containers-common
  git
  go
  go-md2man
  gpgme
  libseccomp
  man-db
  shadow
  sqlite
  systemd
)
# https://github.com/containers/podman/issues/13297
options=(!lto)
source=(
  "git+$url?signed#tag=v$pkgver"
)
# See the release-keys repository
# https://github.com/containers/release-keys
validpgpkeys=(
  B7DBDCA456F7335E91F1C25CD3624C551D0515C4  # Matthew Heon <mheon@redhat.com>
  7CE1E6F8C90CB53E7E4D8F2D502E08DB0BBF8EEE  # Ashley Cui <acui@redhat.com>
  9E33DD8704CC03E2DEB84D9A1C1EDD7CC7C3A0DD  # Lokesh Mandvekar <lsm5@redhat.com>
)
sha512sums=('37de59bafa8feeaa00d20db85dc630c06594566d42b7182bdc4bde8de391f9c56fd1844f2989aced737debcfd4ea9f1abc732f4cd19055017e5e2819dd6c004d')
b2sums=('ab8a6d60f577d8c2b008ea44436cde9b81deb4fd162a69b37de079e9b44a150bd78d1cb78020fe7da207133a7ffb4124ee3a7ce3779ef127334e059975a4e40f')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie"
  export GOPATH="${srcdir}"

  make EXTRA_LDFLAGS='-linkmode=external' PREFIX=/usr -C $pkgbase
  make docker-docs -C $pkgbase
}

check() {
  local container_libs_version="$(</usr/share/containers/container-libs.version)"
  local project_container_libs_version="$(sed --regexp-extended --quiet 's|.*go.podman.io/common v(.*)|\1|p' "$pkgbase/go.mod")"

  printf '%s %s (container-libs %s) - system container-libs %s\n' "$pkgbase" "$pkgver" "${project_container_libs_version%.*}" "${container_libs_version%.*}"

  if (( $(vercmp "${container_libs_version%.*}" "${project_container_libs_version%.*}") != 0 )); then
    printf '%s %s requires container-libs/common in version %s but we have version %s\n' "$pkgbase" "$pkgver" "${project_container_libs_version%.*}" "${container_libs_version%.*}"
    exit 1
  fi
}

package_podman() {
  depends=(
    catatonit
    conmon
    containers-common
    oci-runtime
    glibc
    nftables
    gpgme libgpgme.so
    libgcc
    libseccomp libseccomp.so
    passt
    shadow
    sqlite
  )
  optdepends=(
    'apparmor: for AppArmor support'
    'btrfs-progs: support btrfs backend devices'
    'fuse-overlayfs: for deprecated storage driver in rootless environment'
    'podlet:  Generate Podman Quadlet files from a Podman command, compose file, or existing object'
    'podman-compose: for docker-compose compatibility'
    'podman-desktop: GUI and tray to manage Podman containers (and Kubernetes pods)'
    'podman-docker: for Docker-compatible CLI'
  )

  make install install.completions DESTDIR="$pkgdir" PREFIX=/usr LIBEXECDIR=/usr/lib -C $pkgbase
}

package_podman-docker() {
  pkgdesc='Emulate Docker CLI using podman'
  depends=(
    podman
    sh
  )
  conflicts=(docker)
  provides=(docker)

  make install.docker-full DESTDIR="$pkgdir" PREFIX=/usr -C $pkgbase
}
